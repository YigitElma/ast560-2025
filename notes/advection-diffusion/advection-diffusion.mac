kill(all)$

load("interpol")$
load("draw")$

/** Construct a Lagrange interpolation polynomial on a 3-cell stencil */
p : [ [-dx, fL], [ 0, f0], [ dx, fR] ]$
fh : lagrange(p, varname='x)$

diff1 : subst(x=0, diff(fh, x, 1)), fullratsimp$
diff2 : subst(x=0, diff(fh, x, 2)), fullratsimp$

fp : subst(x=dx/2, fh)$
fm : subst(x=-dx/2, fh)$

tay(N) := T[0] + sum(T[i]*x^i/factorial(i),i,1,N)$

tay5 : tay(5)$

slist : [f0 = subst(x=0,tay5), fR = subst(x=dx,tay5), fL = subst(x=-dx,tay5)]$
eDiff1 : subst(slist, diff1), expand$
eDiff2 : subst(slist, diff2), expand$

eFp : subst(slist, fp) - subst(x=dx/2,tay5), expand$
eFm : subst(slist, fm) - subst(x=-dx/2,tay5), expand$

/** Fourier analysis */
fur(x) := exp(%i*k*x)$

slist : [f0 = subst(x=0,fur(x)), fR = subst(x=dx,fur(x)), fL = subst(x=-dx,fur(x))]$
fDiff1 : subst(slist, diff1), expand$
fDiff2 : subst(slist, diff2), expand$

kbar1 : subst(dx=1, realpart(fDiff1/%i))$
plot2d([k, kbar1], [k,0,%pi], grid2d,
  [style, [lines, 2, red], [lines, 2, blue]],
  [xlabel, "k dx"],
  [pdf_file, "a560-kbar-diff1.pdf"],
  [gnuplot_pdf_term_command, "set term pdfcairo color solid lw 3 size 17.2 cm, 12.9 cm font \",24\""]  
  )$

kbar2 : subst(dx=1, realpart(fDiff2))$
plot2d([k^2, -kbar2], [k,0,%pi], grid2d,
  [style, [lines, 2, red], [lines, 2, blue]],
  [xlabel, "k dx"],
  [pdf_file, "a560-kbar-diff2.pdf"],
  [gnuplot_pdf_term_command, "set term pdfcairo color solid lw 3 size 17.2 cm, 12.9 cm font \",24\""]  
  )$

/** Patholigical interpolation using uniform points */
g(x) := 1/(1 + 10*x^2)$
N : 11$
dx : 2/(N-1)$
xn : makelist(-1+dx*i, i, 0, N-1)$
pn : makelist([xn[i], g(xn[i])], i, 1, N)$
gn : lagrange(pn, varname='x), expand$

xcn : makelist(cos(i*%pi/(N+1)), i, 0, N+1)$
pcn : float(makelist([xcn[i], g(xcn[i])], i, 1, length(xcn)))$
gcn : lagrange(pcn, varname='x), expand$

/* Uniform */
plot2d([[discrete, pn], g, gn], [x,-1,1],
  [style, points, [lines, 2, red], [lines, 2, black]],
  [legend, "Nodal", "Exact", "Interp"],
  [pdf_file, "a560-patho-interp.pdf"],
  [gnuplot_pdf_term_command, "set term pdfcairo color solid lw 3 size 17.2 cm, 12.9 cm font \",24\""]
  )$

/* Cheb */
plot2d([[discrete, pcn], g, gcn], [x,-1,1],
  [style, points, [lines, 2, red], [lines, 2, black]],
  [legend, "Nodal", "Exact", "Cheb"],
  [color, blue, red, black],
  [pdf_file, "a560-patho-cheb.pdf"],
  [gnuplot_pdf_term_command, "set term pdfcairo color solid lw 3 size 17.2 cm, 12.9 cm font \",24\""]  
  )$

/* Cheb diff */
plot2d([diff(g(x),x), diff(gcn,x)], [x,-1,1],
  [style, lines, lines],
  [legend, "Exact", "Cheb"],
  [color, blue, red],
  [pdf_file, "a560-patho-cheb.pdf"],
  [gnuplot_pdf_term_command, "set term pdfcairo color solid lw 3 size 17.2 cm, 12.9 cm font \",24\""]    
  )$
